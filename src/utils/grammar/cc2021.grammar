PROGRAM : STATEMENT
        | FUNCLIST
        | &
           
FUNCLIST : FUNCDEF FUNCLIST2
        
FUNCLIST2 : FUNCLIST
            | &
            
FUNCDEF : "def" "ident" "(" PARAMLIST ")" "{" STATELIST "}"
          
PARAMLIST : TYPE "ident" PARAMLIST2
          | &
       
PARAMLIST2 : "," PARAMLIST
             | &
           
TYPE : "int"
         | "float"
         | "string"
          
STATEMENT : VARDECL ";"
          | ATRIBSTAT ";"
          | PRINTSTAT ";"
          | READSTAT ";"
          | RETURNSTAT ";"
          | IFSTAT
          | FORSTAT
          | "{" STATELIST "}"
          | "break" ";"
          | ";"
            
VARDECL : TYPE "ident" ARRAY_OP
         
ARRAY_OP : "[" "int_constant" "]" ARRAY_OP
           | &
          
ATRIBSTAT : LVALUE "=" RIGHT_ATRIB
        
RIGHT_ATRIB : EXPR_OR_FCALL
            | ALLOCEXPRESSION                      

EXPR_OR_FCALL: '+' FACTOR OPT_UNARY OPT_ARITHM OPT_CMP_EXPR
                      | '-' FACTOR OPT_UNARY OPT_ARITHM OPT_CMP_EXPR
                      | "int_constant" OPT_UNARY OPT_ARITHM OPT_CMP_EXPR
                      | "float_constant" OPT_UNARY OPT_ARITHM OPT_CMP_EXPR
                      | "string_constant" OPT_UNARY OPT_ARITHM OPT_CMP_EXPR
                      | "null" OPT_UNARY OPT_ARITHM OPT_CMP_EXPR
                      | "(" NUMEXPRESSION ")" OPT_UNARY OPT_ARITHM OPT_CMP_EXPR
                      | "ident" AFTER_IDENT

AFTER_IDENT: OPT_ALLOC_EXPR OPT_UNARY OPT_ARITHM OPT_CMP_EXPR
            | "(" PARAMLISTCALL ")"
      
PARAMLISTCALL : "ident" PARAMLISTCALL2
              | &
   
PARAMLISTCALL2 : "," PARAMLISTCALL
                 | &
          
PRINTSTAT : "print" EXPRESSION
           
READSTAT : "read" LVALUE
         
RETURNSTAT : "return"
             
IFSTAT : "if" "(" EXPRESSION ")" "{" STATELIST "}" ELSESTAT
           
ELSESTAT : "else" "{" STATELIST "}"
         | &
            
FORSTAT : "for" "(" ATRIBSTAT ";" EXPRESSION ";" ATRIBSTAT ")" "{" STATELIST "}"
          
STATELIST : STATEMENT OPT_STATELIST
      
OPT_STATELIST : STATELIST
              | &
    
ALLOCEXPRESSION : "new" TYPE "[" NUMEXPRESSION "]" OPT_ALLOC_EXPR
   
OPT_ALLOC_EXPR : "[" NUMEXPRESSION "]" OPT_ALLOC_EXPR
                 | &
         
EXPRESSION : NUMEXPRESSION OPT_CMP_EXPR

OPT_CMP_EXPR : CMP NUMEXPRESSION
                    | &
             
CMP : "<"
       | ">"
       | "<="
       | ">="
       | "=="
       | "!="
      
NUMEXPRESSION : TERM OPT_ARITHM

OPT_ARITHM : ARITHM TERM OPT_ARITHM
                    | &
      
ARITHM : "+"
              | "-"
               
TERM : UNARYEXPR OPT_UNARY_TERM
      
OPT_UNARY_TERM : OPT_UNARY TERM
              | &
       
OPT_UNARY : "*"
             | "/"
             | "%"
          
UNARYEXPR : ARITHM FACTOR
          | FACTOR
             
FACTOR : "int_constant"
       | "float_constant"
       | "string_constant"
       | "null"
       | LVALUE
       | "(" NUMEXPRESSION ")"
             
LVALUE : "ident" OPT_ALLOC_EXPR
